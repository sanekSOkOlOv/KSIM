globals [
  initial-trees   ;; how many trees (green patches) we started with
  fires-extinguished  ;; кількість погашених клітинок

]



breed [firefighters firefighter]
patches-own [ tree-type ]  ;; тип дерева: "normal" або "resistant"


to setup-firefighters
  create-firefighters number-of-firefighters [
    ;; вибрати випадкову порожню клітинку
    move-to one-of patches with [pcolor != red and pcolor != green]
    set color white
    set shape "person"
  ]
end

to move-firefighters
  ask firefighters [
    rt random 360
    fd 1
    ;; якщо поруч є червоні клітинки — гасимо їх
    ask neighbors4 with [pcolor = red] [
      set pcolor black
      set fires-extinguished fires-extinguished + 1
    ]
    ;; якщо сам пожежний випадково зайшов на червону клітинку — теж гасимо
    if [pcolor] of patch-here = red [
      ask patch-here [ set pcolor black ]
      set fires-extinguished fires-extinguished + 1
    ]
  ]
end


to setup
  clear-all
  set fires-extinguished 0  ;; обнулити лічильник
  ;; стандартна ініціалізація дерев
  ask patches [
  if (random 100) < density [
    ;; створюємо дерево
    set tree-type one-of ["normal" "resistant"]
    if tree-type = "normal" [ set pcolor green ]
    if tree-type = "resistant" [ set pcolor lime ]  ;; інший відтінок для вогнестійких
  ]
  if pxcor = min-pxcor [ set pcolor red ]
]

  set initial-trees count patches with [pcolor = green or pcolor = lime]
  ;;set number-of-firefighters 10
  
  setup-firefighters
  reset-ticks
end


to go
  ;; stop the model when done
  if all? patches [ pcolor != red ] [ stop ]
  ;; each burning tree (red patch) checks its 4 neighbors.
  ;; If any are unburned trees (green patches), change their probability
  ;; of igniting based on the wind direction
  ask patches with [ pcolor = red ] [
    ;; ask the unburned trees neighboring the burning tree
   ask neighbors4 with [ pcolor = green or pcolor = lime ][
      let probability probability-of-spread
      ;; compute the direction from you (the green tree) to the burning tree
      ;; (NOTE: “myself” is the burning tree (the red patch) that asked you
      ;; to execute commands)
      let direction towards myself
      ;; the burning tree is north of you
      ;; so the south wind impedes the fire spreading to you
      ;; so reduce the probability of spread
      if direction = 0 [
        set probability probability - south-wind-speed
      ]
      ;; the burning tree is east of you
      ;; so the west wind impedes the fire spreading to you
      ;; so reduce the probability of spread
      if direction = 90 [
        set probability probability - west-wind-speed
      ]
      ;; the burning tree is south of you
      ;; so the south wind aids the fire spreading to you
      ;; so increase the probability of spread
      if direction = 180 [
        set probability probability + south-wind-speed
      ]
      ;; the burning tree is west of you
      ;; so the west wind aids the fire spreading to you
      ;; so increase the probability of spread
      if direction = 270 [
        set probability probability + west-wind-speed
      ]
      if tree-type = "resistant" [
  			set probability probability * 0.66  ;;
			]

      if random 100 < probability [
        set pcolor red ;; to catch on fire
      ]
    ]
    set pcolor red - 3.5 ;; once the tree is burned, darken its color
  ]
  ; рух пожежних
  move-firefighters

  tick ;; advance the clock by one “tick”
end


; Copyright 2006 Uri Wilensky.
; See Info tab for full copyright and license.